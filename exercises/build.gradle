// This root Gradle project is used for:
//
// - Checking that every test suite compiles against its corresponding reference implementation;
// - Checking that every reference implementation passes its corresponding test suite;
// - Checking that every provided starter implementation compiles.
//
// This file is never delivered to Exercism users.

def generatedTestSourceDir = "build/gen/test/java"

subprojects {

  apply plugin: "checkstyle"

  // Add a task that copies test source files into a build directory.
  // All @Disabled annotations are removed during this copying,
  // so that when we run the copied tests, none are skipped and all should execute.
  tasks.register('copyTestsFilteringIgnores', Copy) {
    from "src/test/java"
    into generatedTestSourceDir
    filter { line ->
      line ==~ /.*@Disabled.*/ ? null : line
    }
  }

  afterEvaluate { project ->
    // skip project named 'concept' or 'practice'
    // they are only folders containing exercises
    if(project.name == 'concept' || project.name == 'practice')
      return

    // Add a task that creates Gradle wrappers for all exercises,
    // matching the Gradle version of the project root.
    tasks.register('allWrappers', Wrapper) {
      gradleVersion = "$gradle.gradleVersion"
    }

    sourceSets {
      // Set the directory containing the reference solution as the default source set. Default
      // compile tasks will now run against this source set.
      main {
        java.srcDirs = [".meta/src/reference/java"]
      }

      // Set the directory containing the @Disabled-stripped tests as the default test source set.
      // Default test tasks will now run against this source set.
      test {
        java.srcDirs = [generatedTestSourceDir]
      }

      // Define a custom source set named "starterSource" that points to the starter implementations
      // delivered to users. We can then use the generated "compileStarterSourceJava" task to verify
      // that the starter source compiles as-is.
      starterSource {
        java.srcDirs = ["src/main/java"]
      }

      // Define a custom source set named "starterTest" that points to the tests delivered to users.
      // We can then use the generated "compileStarterTestJava" task to verify that the tests compile
      // cleanly against the starter source.
      starterTest {
        java.srcDirs = ["src/test/java"]
        compileClasspath += starterSource.runtimeClasspath
      }

      // Log the source paths associated with each source set to verify they are what we expect.
      logCompileTaskSourcePath(project, "compileJava")
      logCompileTaskSourcePath(project, "compileTestJava")
      logCompileTaskSourcePath(project, "compileStarterSourceJava")
      logCompileTaskSourcePath(project, "compileStarterTestJava")
    }

    configurations {
      starterSourceImplementation.extendsFrom implementation
      starterTestImplementation.extendsFrom testImplementation
    }

    // configuration of the linter
    checkstyle {
      toolVersion '10.7.0'
      configFile file("checkstyle.xml")
      sourceSets = [project.sourceSets.main, project.sourceSets.test]
    }

    checkstyleTest {
      source = "src/test/java"
    }

    // When running the standard test task, make sure we pre-populate the test source set with the
    // @Disabled-stripped tests.
    compileTestJava.dependsOn(copyTestsFilteringIgnores)

    // Enable test logging when running test task from the console
    test {
      testLogging {
        events "passed", "skipped", "failed"
      }
    }
  }
}

def logCompileTaskSourcePath(Project project, String taskName) {
  project[taskName].doFirst { compileTask ->
    println "  (source = ${compileTask.source.asPath})"
  }
}
